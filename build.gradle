buildscript {
    ext {
        slf4j_version='2.0.0-alpha1'
        logback_version='1.3.0-alpha5'
        raksrinana_utils_version='15.0.0'
        picocli_version = '4.5.1'
        metadata_extractor_version='2.14.0'
        pointlocation6709_version='4.1'
        unirest_version='3.11.00'
        jackson_version='2.11.3'
    }
}

plugins {
    id 'com.github.johnrengelman.shadow' version '6.1.0'
    id 'com.github.ben-manes.versions' version '0.33.0'
    id 'idea'
    id 'java'
    id 'java-library'
    id 'application'
    id 'maven-publish'
    id 'com.novoda.build-properties' version '0.4.1'
    id 'io.freefair.lombok' version '5.2.1'
    id 'com.github.breadmoirai.github-release' version '2.2.12'
}

def secretsFile = new File(".", "secrets.properties")

if (!secretsFile.getParentFile().exists()) {
    secretsFile.mkdirs()
}

if (!secretsFile.exists()) {
    secretsFile.withWriterAppend { w ->
        w << "\n"
    }
}

buildProperties {
    secrets {
        using(file('secrets.properties')).or(System.getenv()).or([
                "GITHUB_USER" : "N/A",
                "GITHUB_TOKEN": "N/A"
        ])
    }
}

group = 'fr.raksrinana'
description = 'NameAsCreated'
sourceCompatibility = targetCompatibility = JavaVersion.VERSION_15

mainClassName = project.mainClass

dependencies {
    api("org.slf4j:slf4j-api:${slf4j_version}")
    api("ch.qos.logback:logback-classic:${logback_version}") {
        exclude group: 'edu.washington.cs.types.checker', module: 'checker-framework'
    }
    api("fr.raksrinana:utils.http:${raksrinana_utils_version}")
    api("com.konghq:unirest-java:${unirest_version}")

    api("com.fasterxml.jackson.core:jackson-core:${jackson_version}")
    api("com.fasterxml.jackson.core:jackson-annotations:${jackson_version}")
    api("com.fasterxml.jackson.core:jackson-databind:${jackson_version}")

    api("com.drewnoakes:metadata-extractor:${metadata_extractor_version}")
    api("us.fatehi:pointlocation6709:${pointlocation6709_version}")

    implementation("info.picocli:picocli:${picocli_version}")
}

repositories {
    maven {
        url = 'https://maven.pkg.github.com/RakSrinaNa/JavaUtils/'
        credentials {
            username buildProperties.secrets['GITHUB_USER'].string
            password buildProperties.secrets['GITHUB_TOKEN'].string
        }
    }
    jcenter()
    mavenCentral()
}

processResources {
    expand project.properties
}

compileJava {
    inputs.property("moduleName", moduleName)
    doFirst {
        options.compilerArgs = [
                '--module-path', classpath.asPath,
        ]
        classpath = files()
    }
}

application {
    mainModule = project.moduleName
    mainClass = project.mainClass
}

java {
    modularity.inferModulePath.set(true)
}

shadowJar {
    archivesBaseName = project.name
    classifier = 'shaded'
}

tasks.withType(JavaCompile) {
    options.encoding = 'UTF-8'
    options.deprecation = true
}

tasks.withType(Wrapper) {
    gradleVersion = project.wrapper_version
}

publishing {
    repositories {
        maven {
            name = "GitHubPackages"
            url = uri("https://maven.pkg.github.com/RakSrinaNa/NameAsCreated")
            credentials {
                username = buildProperties.secrets['GITHUB_USER'].string
                password = buildProperties.secrets['GITHUB_TOKEN'].string
            }
        }
    }
    publications {
        gpr(MavenPublication) {
            from(components.java)
        }
    }
}

githubRelease {
    owner = "RakSrinaNa"
    repo = "NameAsCreated"
    token = buildProperties.secrets['GITHUB_TOKEN'].string
    tagName = "v${project.version}"
    releaseName = "${project.version}"
}
