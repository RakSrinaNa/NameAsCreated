plugins {
    id 'com.github.johnrengelman.shadow' version '5.2.0'
    id "com.github.ben-manes.versions" version '0.27.0'
    id 'java'
    id 'application'
    id 'maven-publish'
    id 'com.novoda.build-properties' version '0.4.1'
    id 'io.freefair.lombok' version '4.1.5'
}

def secretsFile = new File(".", "secrets.properties")

if (!secretsFile.getParentFile().exists()) {
    secretsFile.mkdirs()
}

if (!secretsFile.exists()) {
    secretsFile.withWriterAppend { w ->
        w << "\n"
    }
}

buildProperties {
    secrets {
        using(file('secrets.properties')).or(System.getenv()).or([
                "GITHUB_USER" : "N/A",
                "GITHUB_TOKEN": "N/A"
        ])
    }
}

group = 'fr.raksrinana'
description = 'NameAsCreated'
sourceCompatibility = targetCompatibility = JavaVersion.VERSION_13

ext.moduleName = project.moduleName
mainClassName = project.mainClass

dependencies {
    implementation("org.slf4j:slf4j-api:${project.slf4j_version}")
    implementation("ch.qos.logback:logback-classic:${project.logback_version}") {
        exclude group: 'edu.washington.cs.types.checker', module: 'checker-framework'
    }
    implementation("fr.mrcraftcod:utils.http:${project.raksrinana_utils_version}")

    implementation("com.drewnoakes:metadata-extractor:${project.metadata_extractor_version}")
    implementation("us.fatehi:pointlocation6709:${project.pointlocation6709_version}")

    implementation("com.beust:jcommander:${project.jcommander_version}")
}

repositories {
    maven {
        url = 'https://repo.maven.apache.org/maven2'
    }
    maven {
        url = 'https://jcenter.bintray.com'
    }
    maven {
        url = 'https://maven.mrcraftcod.fr'
    }
}

import org.apache.tools.ant.filters.ReplaceTokens

processResources {
    from 'src/main/resources'
    filter(ReplaceTokens, tokens: [project_version: project.version, project_name: project.name])
}

compileJava {
    inputs.property("moduleName", moduleName)
    doFirst {
        options.compilerArgs = [
                '--module-path', classpath.asPath,
        ]
        classpath = files()
    }
}

jar {
    manifest {
        attributes 'Main-Class': project.mainClass
    }
}

shadowJar {
    baseName = project.name
    classifier = 'shaded'
    version = null
}

tasks.withType(JavaCompile) {
    options.encoding = 'UTF-8'
}

publishing {
    repositories {
        maven {
            name = "GitHubPackages"
            url = uri("https://maven.pkg.github.com/RakSrinaNa/NameAsCreated")
            credentials {
                username = buildProperties.secrets['GITHUB_USER'].string
                password = buildProperties.secrets['GITHUB_TOKEN'].string
            }
        }
    }
    publications {
        gpr(MavenPublication) {
            from(components.java)
        }
    }
}
